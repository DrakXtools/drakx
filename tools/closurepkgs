#!/usr/bin/perl

sub chop_version($) {
    ($_[0] =~ /(.*)-[^-]+-[^-]+/)[0] || $_[0];
}

sub read_depslist {
    my ($file) = @_;
    my $depslist = { 'ordered' => [], 'packages' => {} };
    my $id = 0;

    local *F;
    open F, $file or die "closurepkgs: unable to open ordered dependencies list file $file\n";
    foreach (<F>) {
	my ($name, $size, @deps) = split;
	push @{$depslist->{ordered}}, { id => $id++, name => $name, size => $size, deps => \@deps };
    }
    close F;

    foreach (@{$depslist->{ordered}}) {
	$depslist->{packages}{chop_version($_->{name})} = $_;
    }

    print STDERR "closurepkgs: read " . scalar(@{$depslist->{ordered}}) . " package dependancies\n";

    $depslist;
}

sub read_compsslist {
    my ($file) = @_;
    my %compsslist;

    local *F;
    open F, $file or die "closurepkgs: unable to open compsslist file $file\n";
    foreach (<F>) {
	my ($name, $level) = /^\s*(\S+)\s+(\d+)/;
	$compsslist{$name} = $level;
    }
    close F;

    \%compsslist;
}

sub main {
    my ($depslist_file, $compsslist_file, $lang) = @_;
    my $depslist = read_depslist($depslist_file);
    my $compsslist = read_compsslist($compsslist_file);

    #- DO NOT FORGET TO UPDATE HERE ACCORDING TO gi/perl-install/install_any.pm
    my @pkgs = qw(XFree86 XFree86-glide-module Device3Dfx Glide_V3-DRI Glide_V5 Mesa
		  dhcpcd pump dhcpxd dhcp-client isdn4net isdn4k-utils dev pptp-adsl-fr rp-pppoe ppp ypbind
		  rhs-printfilters lpr cups cups-drivers samba ncpfs ghostscript-utils
		  kernel-pcmcia-cs apmd cdrecord
		  );
    push @pkgs, "XFree86-$_" foreach qw(3DLabs 3dfx 8514 AGX FBDev I128 Mach8 Mach32 Mach64 Mono P9000 Rage128 S3 S3V SVGA VGA16 W32);

    #- closure the list of package to be kept for oem.
    my %closure;
    foreach (@pkgs) {
	$closure{$_} = 1;
	map { $closure{chop_version($_->{name})} = 1 } map { $depslist->{ordered}[$_] } map { split /\|/ } @{$depslist->{packages}{$_}{deps}};
    }
    #- only if dependancy is ok.
    $closure{$_} = 1 foreach qw(xpp kups kisdn);

    #- closure the list of package to always install for oem. (level >= 50)...
    my %install;
    my @force_install;
    if ($depslist->{packages}{"locales-$lang"}) {
	foreach (keys %{$depslist->{packages}}) {
	    push @force_install, $_ if grep { $_ == $depslist->{packages}{"locales-$lang"}{id} } @{$depslist->{packages}{$_}{deps}};
	}
    }
    push @force_install, qw(cups cups-drivers drakprofile draksync irda-utils numlock raidtools reiserfs-utils
			    Mesa Mesa-demos alsa alsa-utils);
    foreach (qw(
		Aurora xawtv kwintv xscreensaver-gl Mesa-demos xmms-mesa bzflag csmash gltron spacecup chromium tuxracer
		), @force_install, grep { $compsslist->{$_} >= 50 } keys %{$depslist->{packages}}) {
	$install{$_} = 1;
	map { $install{chop_version($_->{name})} = 1 } map { $depslist->{ordered}[$_] } map { split /\|/ } @{$depslist->{packages}{$_}{deps}};
    }

    #- remove base packages, which have to be installed, according to basesystem.
    delete $closure{'basesystem'};
    $install{'basesystem'} = 1;
    map { delete $closure{chop_version($_->{name})};
	  $install{chop_version($_->{name})} = 1 } map { $depslist->{ordered}[$_] } map { split /\|/ } @{$depslist->{packages}{'basesystem'}{deps}};

    #- special packages that are to be move to closure always ...
    foreach (qw(kernel-smp kernel-linus kernel-secure hackkernel-smp hackkernel-linus hackkernel-secure
		Aurora xawtv kwintv xscreensaver-gl xmms-mesa bzflag csmash gltron spacecup chromium tuxracer
		)) {
	$depslist->{packages}{$_} or next;
	$closure{$_} = 1;
	delete $install{$_};
    }

    #- dump out the list of package according to the 2 lists defined above.
    my $total_install = 0;
    my $total_closure = 0;
    foreach (@{$depslist->{ordered}}) {
	my $tiny_name = chop_version($_->{name});
	if (exists $install{$tiny_name}) {
	    $total_install += $depslist->{packages}{$tiny_name}{size};
	    print "I:", $depslist->{packages}{$tiny_name}{name}, "\n";
	} elsif (exists $closure{$tiny_name}) {
	    $total_closure += $depslist->{packages}{$tiny_name}{size};
	    print "C:", $depslist->{packages}{$tiny_name}{name}, "\n";
	}
    }
    print "\n\ntotal_install=$total_install\n";
    print "total_closure=$total_closure\n";
}

main(@ARGV);
