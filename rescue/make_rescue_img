#!/usr/bin/perl

use MDK::Common;
use lib "/usr/lib/libDrakX";
use keyboard;

my $tmp = "/tmp/rescue_tmp";
my $rescue = "rescue.sqfs";

if ($>) {
    $ENV{PATH} = "/sbin:/usr/sbin:$ENV{PATH}";
}

BEGIN { undef *_ }
sub __ { print @_, "\n"; system(@_) }
sub _ { __ @_; $? and die }

_ "rm -rf $tmp" if -e $tmp;
_ "mkdir $tmp";
_ 'find . -name "*~" | xargs rm -f';
mkdir_p($tmp . chomp_($_)) foreach cat_("dirs");
_ "cp -a tree/* $tmp";
_ "find $tmp -name .svn | xargs rm -rf";

my $issue = `linux_logo -l`;

$issue .= <<EOF;

		[1;37;40mRescue Disk[0m


$ENV{DISTRIB_DESCR}

Use [1;33;40mloadkeys[0m to change your keyboard layout (eg: loadkeys fr)
Use [1;33;40mmodprobe[0m to load modules (eg: modprobe snd-card-fm801)
Use [1;33;40mdrvinst[0m to install drivers according to detected devices
Use [1;33;40mlsparts[0m to list your partitions with types
Use [1;33;40mstartssh[0m to start an ssh daemon
Use [1;33;40mrescue-gui[0m to go back to the rescue menu

EOF

output("$tmp/etc/issue", $issue);

_ "../tools/install-xml-file-list list.xml $tmp";

# XXX: prevent this from being added to begin with
_ "rm -rf $tmp/usr/share/locale/";

_ "cp /bin/busybox.static $tmp/usr/bin/busybox";
my @busybox_links = split("\n", `$tmp/usr/bin/busybox --list-full`);
_ "test -e $tmp/$_ || ln $tmp/usr/bin/busybox $tmp/$_" foreach @busybox_links;
# required by busybox
_ "mkdir $tmp/dev";

my %keytable_conflicts;
my @less_important_keytables = qw(am_old am_phonetic no-dvorak de-latin1);
foreach (keyboard::loadkeys_files(sub { warn @_ })) {
    my ($dir, $fname) = (dirname($_), basename($_));
    my ($name) = $fname =~ /(.*)\.map\.gz/ or next;
    next if member($name, @less_important_keytables);
    if (my ($short2, $short) = $name =~ m|((.+?)[\W_][^\W_]*)|) {
	$keytable_conflicts{$short} && $short2 ne $name and $short = $short2;
	$keytable_conflicts{$short} and warn("conflict between $keytable_conflicts{$short} and $name for short name $short (choosing the first)\n"), next;
	$keytable_conflicts{$short} = $name;
	# create the short name based on the long one
	symlinkf($fname, "$tmp$dir/$short.map.gz");
    }
}

foreach (cat_("aliases")) {
    chomp; my ($f, $dest) = split;
    symlink $dest, "$tmp$f";
}


#if (my ($LANGUAGE) = map { if_(/LANGUAGE_(.*)/, $1) } keys %ENV) {
my $LANGUAGE = "C";
    substInFile {
	$_ = "export LANGUAGE=$LANGUAGE\n" . "export LC_ALL=$LANGUAGE\n" if /^#LANGUAGE/;	
    } "$tmp/etc/rc.sysinit";
#}

exit 0 if $ARGV[0];

_ "mksquashfs $tmp $rescue -all-root -noappend >/dev/null";
_ "chmod 755 $rescue";
_ "rm -rf $tmp";
