#!/usr/bin/perl
# DrakxDM -- Display Manager chooser
# Copyright (C) 2003-2008 Mandriva (tvignaud@mandriva.com)
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

use strict;
use lib qw(/usr/lib/libDrakX);

use standalone;     #- warning, standalone must be loaded very first, for 'explanations'
use common;
use any;
use interactive;
use services;
use run_program;

$ugtk2::wm_icon = "/usr/share/mcc/themes/default/drakedm-mdk.png";

my $in = 'interactive'->vnew('su');

my $cfg_file = '/etc/sysconfig/desktop';

my @list = map {
    my %l = map { /(\S+)=(.*)/ } cat_($_);
    \%l;
} sort(glob("/etc/X11/dm.d/*.conf"));

my @_DESCRIPTIONS_for_i18n = (
    N("GDM (GNOME Display Manager)"),
    N("KDM (KDE Display Manager)"),
    N("XDM (X Display Manager)"),
);

if (!$::expert) {
    @list = grep { -e $_->{EXEC} } @list;
}

my ($dm_NAME) = cat_($cfg_file) =~ /^DISPLAYMANAGER=(.*)/m;
my $dm = (find { uc($_->{NAME}) eq uc($dm_NAME) } @list) || $list[0];

start:
if ($in->ask_from(N("Choosing a display manager"),
                               formatAlaTeX(N("X11 Display Manager allows you to graphically log
into your system with the X Window System running and supports running
several different X sessions on your local machine at the same time.")),
                               [
                                {
                                 allow_empty_list => 1,
                                 list => \@list,
                                 val => \$dm,
                                 type => 'list',
                                 format => sub { translate($_[0]{DESCRIPTION}) },
                                }
                               ]
                              )
   ) {
    $in->do_pkgs->ensure_is_installed($dm->{PACKAGE}, $dm->{EXEC}) or goto start;
    addVarsInSh($cfg_file, { DISPLAYMANAGER => $dm->{NAME} });
    log::explanations(qq(Switching to "$dm->{NAME}" display manager));
    if (any::running_window_manager()) {
        $in->ask_yesorno('', N("The change is done, do you want to restart the dm service?"), 1) and
				$in->ask_yesorno('', N("You are going to close all running programs and lose your current session. Are you really sure that you want to restart the dm service?"), 1) and
                      run_program::raw({ detach => 1 }, '/etc/rc.d/init.d/dm', '>', '/dev/null', '2>', '/dev/null', 'restart');
    }
}

$in->exit(0);
