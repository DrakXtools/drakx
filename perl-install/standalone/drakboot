#!/usr/bin/perl

# DrakBoot
# $Id$
# Copyright (C) 2001-2003 MandrakeSoft
# Yves Duret, Thierry Vignaud
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

use strict;
use diagnostics;
use lib qw(/usr/lib/libDrakX);

use standalone; #- warning, standalone must be loaded very first, for 'explanations'
use c;
use common;
use interactive;
use any;
use bootloader;
use detect_devices;
use fsedit;
use fs;
use Config;
use POSIX;
use Xconfig::various;
use log;

my $splash_working = 1;

my $in = 'interactive'->vnew('su');

if (!$in->isa('interactive::gtk') || any { /^--boot$/ } @ARGV) {
    lilo_choice();
    $in->exit(0);
}
require ugtk2;
ugtk2->import(qw(:helpers :wrappers :create));

my $no_bootsplash;
my $x_mode = Xconfig::various::runlevel() == 5;
my $splash_mode = 1;
my $auto_mode = any::get_autologin();
my $lilogrub = bootloader::detect_bootloader();
my $switch_theme = '/usr/share/bootsplash/scripts/switch-themes';

my $w = ugtk2->new(N("Boot Style Configuration"));
my $window = $w->{window};
$::main_window = $w->{rwindow} if !$::isEmbedded;

$window->signal_connect(delete_event => sub { ugtk2->exit(0) });
unless ($::isEmbedded) {
    $window->set_border_width(2);

    ### menus definition
    # the menus are not shown but they provides shiny shortcut like C-q
    my @menu_items = ([ N("/_File"), undef, undef, undef, '<Branch>' ],
                      [ N("/File/_Quit"), N("<control>Q"), sub { ugtk2->exit(0) }, undef, '<Item>' ],
                     );
    create_factory_menu($w->{rwindow}, @menu_items);
    ######### menus end
}

my $user_combo = new Gtk2::OptionMenu;
$user_combo->set_popdown_strings(list_users());
$user_combo->entry->set_text($auto_mode->{autologin}) if $auto_mode->{autologin};
my $desktop_combo = new Gtk2::OptionMenu;
$desktop_combo->set_popdown_strings(split(' ', `/usr/sbin/chksession -l`));
$desktop_combo->entry->set_text($auto_mode->{desktop}) if $auto_mode->{desktop};

my %themes = 	('path' => '/usr/share/bootsplash/themes/',
                'default' => 'Mandrake',
                'def_thmb' => '/usr/share/libDrakX/pixmaps/nosplash_thumb.png',
                'lilo' => {'file' => '/lilo/message',
                           'thumb' => '/lilo/thumb.png' },
                'boot' => {'path' => '/images/',
                                #'thumb'=>'/images/thumb.png',
                          },
               );
my $cur_res = `/usr/share/bootsplash/scripts/detect-resolution`;
#- verify that current resolution is ok
if (member($cur_res, qw( 785 788 791 794))) {
    ($cur_res) = $bootloader::vga_modes{$cur_res} =~ /^([0-9x]+).*?$/;
} else {
    $no_bootsplash = 1; #- we can't select any theme we're not in Framebuffer mode :-/
}

#- and check that lilo is the correct loader
$no_bootsplash ||= $lilogrub ne 'LILO';
do { $no_bootsplash = 0; $cur_res = '800x600' } if $::testing;
my @thms;
my @boot_thms = if_(!$themes{default}, qw(default));
chdir($themes{path}); #- we must change directory for correct @thms assignement
foreach (all('.')) {
    if (-d $themes{path} . $_ && m/^[^.]/) {
        push @thms, $_;
        -f $themes{path} . $_ . $themes{boot}{path} . "bootsplash-$cur_res.jpg" and push @boot_thms, $_;
    }
    #       $_ eq $themes{'defaut'} and $default = $themes{'defaut'};
}
my %combo = ('thms' => '', 'lilo' => '', 'boot' => '');
foreach (keys(%combo)) {
    $combo{$_} = gtkset_size_request(Gtk2::OptionMenu->new, 10, -1);
}

do { $combo{boot}->set_popdown_strings(@boot_thms); $combo{boot}->entry->set_text($themes{default})} if !$no_bootsplash;

my $boot_pic = gtkcreate_img($themes{def_thmb});
change_image($boot_pic, $themes{default});

my $thm_button = new Gtk2::Button(N("Install themes"));
my $logo_thm = new Gtk2::CheckButton(N("Display theme\nunder console"));
my $B_create = new Gtk2::Button(N("Create new theme"));
my $keep_logo = 1;
$logo_thm->set_active(1);
$logo_thm->signal_connect(clicked => sub { invbool(\$keep_logo) });
$B_create->signal_connect(clicked => sub {
                              system('/usr/sbin/draksplash ');
                          });
			  
#- ******** action to take on changing combos values

$no_bootsplash == 0 
  and $combo{boot}->entry->signal_connect(changed => sub { change_image($boot_pic, $combo{boot}->entry->get_text) });
my ($x_box,$splash_box);

gtkadd($window,
       gtkpack_(new Gtk2::VBox(0,0),
                 #Splash Selector
                 if_($splash_working,
                 0, gtkadd(my $thm_frame = new Gtk2::Frame(N("Graphical boot theme selection")),
                         gtkpack(gtkset_border_width(new Gtk2::VBox(0, 5), 5),
		         gtksignal_connect(gtkset_active(new Gtk2::CheckButton(N("Use graphical boot")), $splash_mode), 
			          clicked => sub {
		                 	 $splash_box->set_sensitive(!$splash_mode);
		                  	$splash_mode = !$splash_mode;
		                  }),
				  gtkpack__(gtkset_sensitive($splash_box = Gtk2::HBox->new(0, 0), $splash_mode),
				  gtkpack__(new Gtk2::VBox(0, 5),
                                            N("\nTheme"),
                                            $combo{boot},
                                            $logo_thm),
                                  Gtk2::VSeparator->new,
                                  gtkpack__(new Gtk2::VBox(0, 5),
                                            $boot_pic))
                       ),
                    ),
		 ),
                 0, gtkadd(new Gtk2::Frame(N("System mode")),
                        gtkpack__(new Gtk2::VBox(0, 5),
                                  gtksignal_connect(gtkset_active(new Gtk2::CheckButton(N("Launch the graphical environment when your system starts")), $x_mode), clicked => sub {
                                                        $x_box->set_sensitive(!$x_mode);
                                                        $x_mode = !$x_mode;
                                                    }),
                                  gtkpack__(gtkset_sensitive($x_box = Gtk2::VBox->new(0, 0), $x_mode),
                                            gtkpack__(Gtk2::VBox->new(0, 0),
                                                      my @auto_buttons = gtkradio((N("Yes, I want autologin with this (user, desktop)")) x 2, N("No, I don't want autologin")),
                                                     ),
                                            gtkpack__(
                                                      my $auto_box = Gtk2::HBox->new,
                                                      gtkpack(
                                                              Gtk2::VBox->new,
                                                              Gtk2::Label->new(N("Default user")),
                                                              Gtk2::Label->new(N("Default desktop")),
                                                             ),
                                                      gtkpack(
                                                              Gtk2::VBox->new,
                                                              $user_combo,
                                                              $desktop_combo
                                                             ),
                                                     )
                                           )
                                 )
                       ),
                 1, Gtk2::VBox->new,
                0, create_okcancel({
                                    cancel_clicked => sub { ugtk2->exit(0) },
                                    ok_clicked => sub {
                                              Xconfig::various::runlevel($x_mode ? 5 : 3);
                                              updateAutologin();
					      $no_bootsplash or update_bootsplash($combo{boot}->entry->get_text, $keep_logo);
                                              ugtk2->exit(0);
                                          }
                                   },
                       )
               )
      );

$auto_buttons[0]->signal_connect('toggled' => sub { $auto_box->set_sensitive($auto_buttons[0]->get_active) });
$auto_buttons[1]->signal_connect('toggled' => sub { $auto_box->set_sensitive(!$auto_buttons[1]->get_active) });
$auto_buttons[0]->set_active(1) if $auto_mode->{autologin};
$auto_buttons[1]->set_active(1) if !$auto_mode->{autologin};

$x_box->set_sensitive($x_mode);
$auto_box->set_sensitive($auto_mode->{autologin} ? 1 : 0);
$window->show_all;
#$no_bootsplash and $thm_frame->set_sensitive(0);
$thm_frame->hide if !$splash_working;
gtkflush();
$w->main;
$in->exit(0);



sub lilo_choice() {
    my $bootloader = bootloader::read(); 
    
    my ($all_hds) = fsedit::get_hds();
    my $fstab = [ fsedit::get_all_fstab($all_hds) ];
    fs::merge_info_from_fstab($fstab, '', 0, undef);

  ask:
    eval { any::setupBootloader($in, $bootloader, $all_hds, $fstab, $ENV{SECURE_LEVEL}) };
    my $loader = arch() =~ /ppc/ ? "Yaboot" : "LILO";
    my $err = $@;
    if ($err) {
        $in->ask_warn('', 
                      [ N("Installation of %s failed. The following error occured:", $loader), $err ]);
        goto ask;
    }
}



#-------------------------------------------------------------
# launch autologin functions
#-------------------------------------------------------------

sub updateAutologin() {
    my ($usern, $deskt) = ($user_combo->entry->get_text, $desktop_combo->entry->get_text);
    $::testing and return;
    if ($auto_buttons[0]->get_active) {
        any::set_autologin($usern, $deskt);
    } else {
        any::set_autologin();
    }
}

sub update_bootsplash {
    my ($theme, $keep_logo) = @_;
    if (-x $switch_theme) {
	my $val = $keep_logo ? 'theme' : 'no';
	if ($::testing) {
	    print "substInFile { s/^LOGO_CONSOLE=.*/LOGO_CONSOLE=$val/ } /etc/sysconfig/bootsplash\n";
	    print "system($switch_theme, $theme)\n"
	} else {
	    substInFile { s/^LOGO_CONSOLE=.*/LOGO_CONSOLE=$val/ } "/etc/sysconfig/bootsplash";
	    system($switch_theme, $theme)
	}
    }
}

sub change_image {
    my ($boot_pic, $val) = @_;
    my $img_file = $themes{path} . $val . $themes{boot}{path}."bootsplash-$cur_res.jpg";
    -f $img_file or return;
    my $boot_pixbuf = gtkcreate_pixbuf($img_file);
    $boot_pixbuf = $boot_pixbuf->scale_simple(300, 200, 'nearest');
    $boot_pic->set_from_pixbuf($boot_pixbuf);
}
