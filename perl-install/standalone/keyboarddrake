#!/usr/bin/perl

use lib qw(/usr/lib/libDrakX);

use standalone;     #- warning, standalone must be loaded very first, for 'explanations'

use interactive;
use keyboard;
use Xconfigurator_consts;
use common;
use c;

$::isEmbedded = ($::XID, $::CCPID) = "@ARGV" =~ /--embedded (\w+) (\w+)/;
local $_ = join '', @ARGV;

/-h/ and die _("usage: keyboarddrake [--expert] [keyboard]\n");

$::expert = /-expert/;

print "[$::expert]\n";
my $keyboard='';
if ($::expert) { ($keyboard) = grep { !/^-/ } @ARGV;}
print "[$keyboard]\n";
my $in = 'interactive'->vnew('su', 'keyboard');

begin:
$::isEmbedded and kill USR2, $::CCPID;
$keyboard ||= $in->ask_from_listf_(_("Keyboard"),
				   _("Please, choose your keyboard layout."),
				   \&keyboard::keyboard2text,
				   [ keyboard::keyboards() ],
				   keyboard::read());
if ($keyboard) {
    keyboard::keyboard2text($keyboard) or die "bad keyboard $keyboard\n";

    my $isNotDelete = $::expert && !$in->ask_yesorno("BackSpace", _("Do you want the BackSpace to return Delete in console?"), 1);

    my $kmap = keyboard::keyboard2kmap($keyboard);
    system('loadkeys', $kmap);

    my $xkb = keyboard::keyboard2xkb($keyboard);
    system('setxkbmap', $xkb);

    my $f = "/etc/X11/XF86Config";
    my $g = "/etc/X11/XF86Config-4";

    substInFile {
	if (/^Section\s+"Keyboard"/ .. /^EndSection/) {
	    s|^(\s*XkbLayout\s+).*|$1"$xkb"| 
	      and $_ .= join '', map { "    $_\n" } @{$Xconfigurator::xkb_options{$xkb} || []};
	    $_ = '' if m,^(\s*(XkbVariant|XkbOptions)\s+),; # remove existing one
	}
    } $f if -e $f && !$::testing;

    substInFile {
	if (/Identifier\s+"Keyboard1"/ .. /^EndSection/) {
	    s|^(\s*Option\s+"XkbLayout"\s+).*|$1"$xkb"| 
	      and $_ .= join '', map { /(\S+)(.*)/; qq(    Option "$1" $2\n) } @{$Xconfigurator::xkb_options{$xkb} || []};
	    $_ = '' if m,^(\s*Option\s+"(XkbVariant|XkbOptions)"\s+),; # remove existing one
	}
    } $g if -e $g && !$::testing;

    keyboard::write('', $keyboard, $isNotDelete);
}

if ($::isEmbedded) {
    kill(USR1, $::CCPID);
    $keyboard = '';
    goto begin;
} else {
    $in->exit(0);
}
